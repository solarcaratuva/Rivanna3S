## CMake for the PowerBoard module
# Builds the `powerboard` executable from the sources in this directory.

cmake_minimum_required(VERSION 3.0)

project(PowerBoard LANGUAGES CXX C)

# Sources and headers in this module
set(POWERBOARD_INC_DIR ${CMAKE_CURRENT_LIST_DIR}/Inc)
set(POWERBOARD_SRC_DIR ${CMAKE_CURRENT_LIST_DIR}/Src)
set(CORE_SRC_DIR ${CMAKE_SOURCE_DIR}/Core/Src)

file(GLOB POWERBOARD_SOURCES CONFIGURE_DEPENDS
	"${POWERBOARD_SRC_DIR}/*.cpp"
)

add_executable(powerboard ${POWERBOARD_SOURCES})

# Pull in the bare-metal support files generated by CubeMX (system clock +
# syscalls stubs) so the HAL has its globals resolved.
target_sources(powerboard
	PRIVATE
		${CORE_SRC_DIR}/system_stm32h7xx.c
		${CORE_SRC_DIR}/syscalls.c
		${CORE_SRC_DIR}/sysmem.c
)

# Export the module's public include dir
target_include_directories(powerboard
	PUBLIC
		${POWERBOARD_INC_DIR}
)

# Pull in the shared drivers/modules (plus MCU flags/defs for Cortex-M7).
target_link_libraries(powerboard PRIVATE common mcu_flags)

# Emit a raw binary alongside the ELF so it can be flashed directly.
if(NOT CMAKE_OBJCOPY)
	find_program(CMAKE_OBJCOPY arm-none-eabi-objcopy)
endif()
if(NOT CMAKE_OBJCOPY)
	message(FATAL_ERROR "objcopy not found; cannot convert ELF to BIN")
endif()

add_custom_command(TARGET powerboard POST_BUILD
	COMMAND ${CMAKE_OBJCOPY} -O binary $<TARGET_FILE:powerboard> $<TARGET_FILE_DIR:powerboard>/powerboard.bin
	COMMENT "Generating powerboard.bin"
	VERBATIM
)
